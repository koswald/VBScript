
'fixture for ../EncodingAnalyzer.spec.vbs

'create a test file for each of six file formats

With New FileCreator
    .CreateFiles
End With

Class FileCreator
    Private fso, baseName

    Sub Class_Initialize
        Set fso = CreateObject("Scripting.FileSystemObject")
        baseName = fso.GetBaseName(fso.GetBaseName(WScript.ScriptName)) 'using GetBaseName twice returns EncodingAnalyzer from EncodingAnalyzer.FileGen.vbs, for example, and will also return EncodingAnalyzer from EncodingAnalyzer.vbs
    End Sub

    Sub CreateFiles
        CreateFile 13, 10, 0, 0, "Ascii"
        CreateFile &Hff, &Hfe, 0, 0, "UTF16LE"
        CreateFile &Hfe, &Hff, 0, 0, "UTF16BE"
        CreateFile &H2b, &H2f, &H76, 0, "UTF7"
        CreateFile &Hef, &Hbb, &Hbf, 0, "UTF8"
        CreateFile 0, 0, &Hfe, &Hff, "UTF32"
    End Sub

    Sub CreateFile(byte0, byte1, byte2, byte3, format)
        Const ForWriting = 2
        Const CreateNew = True
        Dim i, stream, fileObj, fileName
        fileName = baseName & "." & format & ".txt"

        'clear the read-only attribute

        On Error Resume Next 'continue if the file does not exist
            Set fileObj = fso.GetFile(fileName)
            fileObj.Attributes = fileObj.Attributes - 1
            Set fileObj = Nothing
        On Error Goto 0

        'create or recreate/overwrite the file

        Set stream = fso.OpenTextFile(fileName, ForWriting, CreateNew)
        For i = 0 To 3
            Execute("stream.Write Chr(byte" & i & ")")
        Next
        stream.WriteBlankLines(2)
        stream.WriteLine "Fixture for ../EncodingAnalyzer.spec.vbs"
        stream.WriteLine
        stream.WriteLine "This file was autogenerated by " & WScript.ScriptName
        stream.WriteLine "with the Byte Order Mark (BOM) of a file in " & format & " format."
        stream.Close
        Set stream = Nothing

        'set the read-only attribute

        Set fileObj = fso.GetFile(fileName)
        fileObj.Attributes = fileObj.Attributes + 1
        Set fileObj = Nothing
    End Sub

    Sub Class_Terminate
        Set fso = Nothing
    End Sub
End Class
