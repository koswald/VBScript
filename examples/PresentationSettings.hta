<html>
<head>
    <hta:application
        icon="filemgmt.dll"
        innerBorder="No"
        scroll="No">
</head>
<body>
    <script language="VBScript">
        Sub PresentationMode
            watcher.Watch = True
            document.Title = "Presentation mode is on"
            Enable offButton
            Disable onButton
            PublishStatus "Presentation"
        End Sub
        Sub NormalMode
            watcher.Watch = False
            document.Title = "Presentation mode is off"
            Enable onButton
            Disable offButton
            PublishStatus "Normal"
        End Sub
        Sub ChargerMode
            sh.Run "rundll32 user32.dll,LockWorkStation",, synchronous
            app.Sleep 1000
            watcher.MonitorOff
            app.Sleep 1000
            PresentationMode
            PublishStatus "Presentation"
        End Sub
        Sub Disable(htmlElement) : htmlElement.disabled = True : End Sub
        Sub Enable(htmlElement) : htmlElement.disabled = False : End Sub
        Sub PublishStatus(newStatus)
            Dim folder : folder = sh.ExpandEnvironmentStrings("%AppData%\VBScripting")
            If Not fso.FolderExists(folder) Then fso.CreateFolder folder
            Dim statusFile : statusFile = folder & "\" & app.GetBaseName & ".status"
            Dim stream : Set stream = fso.OpenTextFile(statusFile, ForWriting, CreateNew)
            stream.WriteLine(newStatus)
            stream.Close
            Set stream = Nothing
        End Sub

        'Set the timeoutKey 'Attributes' DWord to 2 to enable 'Console lock display off timeout' in Advanced power options settings, Display section.
        'Set to 1 to disable (default). https://www.windowscentral.com/how-extend-lock-screen-timeout-display-turn-windows-10
        Const enableTimeout = 2
        Const disableTimeout = 1
        Sub SetTimeoutAttributes(newAttribute)
            regUtil.SetDWordValue regUtil.HKLM, timeoutKey, "Attributes", newAttribute
            If disableTimeout = newAttribute Then
                Disable acSeconds
                Disable saveAC
                Disable dcSeconds
                Disable saveDC
            ElseIf disableTimeout = newAttribute Then
                Enable acSeconds
                Enable saveAC
                Enable dcSeconds
                Enable saveDC
            End If
        End Sub
        Function GetTimeoutAttributes
            GetTimeoutAttributes = regUtil.GetDWordValue(regUtil.HKLM, timeoutKey, "Attributes")
        End Function
        Sub OptionChanged
                SetTimeoutAttributes enabler.options(enabler.SelectedIndex).value
        End Sub
        Sub ChangeACTimeout
            sh.Run "cmd /c echo Changing AC VideoCOnLock to " & acSeconds.value & " seconds... & powercfg.exe /SETACVALUEINDEX SCHEME_CURRENT SUB_VIDEO VIDEOCONLOCK " & acSeconds.value & " & echo Done. & echo This setting may require a computer restart. & echo Press any key to exit & pause > nul",, synchronous
            sh.Run "cmd /c echo Updating the current power scheme... & powercfg.exe /S SCHEME_CURRENT & echo Done. Press any key to exit & pause > nul"
            acSeconds.value = ""
        End Sub
        Sub ChangeDCTimeout
            sh.Run "cmd /c echo Changing DC VideoCOnLock to " & dcSeconds.value & " seconds... & powercfg.exe /SETDCVALUEINDEX SCHEME_CURRENT SUB_VIDEO VIDEOCONLOCK " & dcSeconds.value & " & echo Done. & echo This setting may require a computer restart. & echo Press any key to exit & pause > nul",, synchronous
            sh.Run "cmd /c echo Updating the current power scheme... & powercfg.exe /S SCHEME_CURRENT & echo Done. Press any key to exit & pause > nul"
            dcSeconds.value = ""
        End Sub

        Const synchronous = True
        Const ForWriting = 2, CreateNew = True
        Const timeoutKey = "SYSTEM\CurrentControlSet\Control\Power\PowerSettings\7516b95f-f776-4464-8c53-06167f40cc99\8EC4B3A5-6868-48c2-BE75-4F3044BE88A7"
        Dim watcher, sh, regUtil, app, fso
        Dim optionEnabled, optionDisabled
        Dim errMsg

        Sub Window_OnLoad
            self.resizeTo 450, 630
            self.moveTo 830, 0
            Set watcher = CreateObject("VBScripting.Watcher")
            Set app = CreateObject("VBScripting.VBSApp")
            app.Init document
            Dim includer : Set includer = CreateObject("VBScripting.Includer")
            Execute includer.Read("RegistryUtility")
            Set includer = Nothing
            Set regUtil = New RegistryUtility
            Set sh = CreateObject("WScript.Shell")
            sh.CurrentDirectory = app.GetParentFolderName
            Set fso = CreateObject("Scripting.FileSystemObject")

            Set enabler.OnChange = GetRef("OptionChanged")
            enabler.style.marginTop = "10px"
            Set errMsg = document.getElementsByTagName("div")(0)
            If Not watcher.Privileged Then
                enabler.disabled = True
                acSeconds.disabled = True
                dcSeconds.disabled = True
                saveAC.disabled = True
                saveDC.disabled = True
                Dim msg : msg = "Elevated privileges are required to change this setting."
                enabler.Title = msg
                acSeconds.Title = msg
                dcSeconds.Title = msg
            End If
            Set optionDisabled = document.createElement("option")
            Set optionEnabled = document.createElement("option")
            optionDisabled.innerHTML = "Disabled"
            optionEnabled.innerHTML = "Enabled"
            optionDisabled.value = disableTimeout
            optionEnabled.value = enableTimeout
            enabler.insertBefore optionDisabled
            enabler.insertBefore optionEnabled
            If disableTimeout = GetTimeoutAttributes Then
                enabler.SelectedIndex = 0
            ElseIf enableTimeout = GetTimeoutAttributes Then
                enabler.SelectedIndex = 1
            Else
                MsgBox "Sub Window_OnLoad: Unexpected timeout attributes value: " & GetTimeoutAttributes, vbExclamation, document.Title
            End If
            NormalMode
        End Sub
        Sub Window_OnUnload
            watcher.Dispose
            Set watcher = Nothing
            Set sh = Nothing
            Set regUtil = Nothing
            Set fso = Nothing
        End Sub
    </script>
        
    Presentation.vbs may require changes to Windows settings in 
    order to work properly. Specifically, values may need to be 
    increased that control when the following components go to 
    sleep or turn off for both AC (plugged in) and DC (battery).
    <fieldset>
        <legend> Mode</legend>
        <input
            type="button"
            id="offButton"
            value="Normal mode"
            class="wideButton"
            onclick="NormalMode" /><br />
        <input
            type="button"
            id="onButton"
            value="Presentation mode"
            class="wideButton"
            onclick="PresentationMode" /><br />
        <input
            type="button"
            id="chargeButton"
            value="Phone charger mode"
            class="wideButton"
            onclick="ChargerMode" /><br />
    </fieldset>

    <fieldset>
        <legend> Presentation settings</legend>
        <select
            id="enabler"
            title="If enabled, the 'Console lock display off timeout' setting will appear under the Display section in Power Options | Advanced settings, and the AC/DC settings can be changed there or below. If disabled, this setting will not appear in Power Options | Advanced settings.">
        </select> Console lock display off timeout <br />
        <p class="label"> AC screen lock timeout, in seconds</p>
        <input
            type="text"
            id="acSeconds" />
        <input
            type="button"
            id="saveAC"
            value="Save"
            onclick="ChangeACTimeout" /><br />
        <p class="label"> DC screen lock timeout, in seconds</p>
        <input
            type="text"
            id="dcSeconds"/>
        <input
            type="button"
            id="saveDC"
            value="Save"
            onclick="ChangeDCTimeout" /><br />
        <input
            type="button"
            value="Power options advanced settings"
            class="wideButton"
            onclick='sh.Run "control.exe powercfg.cpl,,3"' /><br />
        <input
            type="button"
            value="Power options global settings"
            class="wideButton"
            onclick='sh.Run "control.exe /name Microsoft.PowerOptions /page pageGlobalSettings"' /><br />
        <input
            type="button"
            value="Power options plan settings"
            class="wideButton"
            onclick='sh.Run "control.exe /name Microsoft.PowerOptions /page pagePlanSettings"' /><br />
        <input
            type="button"
            value="Power & sleep settings"
            class="wideButton"
            onclick='sh.Run "ms-settings:powersleep"' /><br />
        <input
            type="button"
            value="Available sleep states"
            class="wideButton"
            onclick='sh.Run "cmd /k powercfg /AvailableSleepStates"' /><br />
    </fieldset>
    <div class="errMsg"></div>
</body>
<style>
    .wideButton {
        width: 100%;
        margin-top: 10px;
    }
    div {
        margin: 10px;
    }
    body {
        font-family: sans-serif;
        background-color: ButtonFace;
        font-size: smaller;
    }
    fieldset {
        padding: 10px;
        border-width: 3px;
    }
    .label {
        margin-bottom: 4px;
        margin-top: 8px;
    }
    .errMsg {
        color: darkred;
    }
</style>
</html>
