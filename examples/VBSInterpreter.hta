<html>
<head>
    <hta:application
        applicationName = "VBScript Statement Interpreter"
        icon="wscript.exe"
        innerBorder="No"
        singleInstance="Yes">
</head>
<body>
    <script language="VBScript">
        Sub Document_OnKeyUp
            If Esc = window.event.keyCode Then
                typedStatement.value = ""
                typedStatement.focus
            ElseIf F1 = window.event.keyCode Then
                ToggleHelp
                typedStatement.focus
            ElseIf F9 = window.event.keyCode Then
                sh.Run "notepad """ & configFile & """"
            ElseIf EnterKey = window.event.keyCode Then
                xecute typedStatement.value
            ElseIf UpArrow = window.event.keyCode Then
                DecrementIndex
                ShowSavedStatement
            ElseIf DownArrow = window.event.keyCode Then
                IncrementIndex
                ShowSavedStatement
            End If
        End Sub

        Sub DecrementIndex : If statementIndex > 0 Then statementIndex = statementIndex - 1 : End If : End Sub
        Sub IncrementIndex : If statementIndex < UBound(Split(savedStatements, delimiter)) Then statementIndex = statementIndex + 1 : End If : End Sub
        Sub ShowSavedStatement : typedStatement.value = Split(savedStatements, delimiter)(statementIndex) : End Sub

        Sub CheckBoxDivOnHover
            autoExecHistDiv.style.cursor = "default"
        End Sub

        Sub xecute(statement)
            Dim result, erred
            showResult = ""
            On Error Resume Next
                ExecuteGlobal statement
                If Err Then
                    result = Err.Description
                    erred = True
                ElseIf Len(showResult) Then
                    result = showResult
                    erred = False
                Else
                    result = "ok"
                    erred = False
                End If
            On Error Goto 0
            AddTableRow statement & " ", result, erred
            If historyStreamStatus = statusWriting Then
                historyStream.WriteLine statement
            End If
            savedStatements = savedStatements & delimiter & statement
            If Left(savedStatements, Len(delimiter)) = delimiter Then
                'remove leading delimiter from the very first statement
                savedStatements = Right(savedStatements, Len(savedStatements) - Len(delimiter))
            End If
            statementIndex = UBound(Split(savedStatements, delimiter)) + 1
            typedStatement.value = ""
            typedStatement.focus
            window.scrollBy 0, 30
        End Sub

        Sub AddTableRow(statement, result, erred)
            Dim row : Set row = table.InsertRow(-1)
            Dim statementCell : Set statementCell = row.InsertCell(-1)
            Dim resultsCell : Set resultsCell = row.InsertCell(-1)
            statementCell.InnerHTML = statement
            resultsCell.InnerHTML = result
            StyleRow row, erred, statementCell, resultsCell
            rowCount = rowCount + 1
        End Sub
            
        'evaluate an expression and show the result
        'by typing "show CBool(-1)" for example
        Sub Show(expressionResult)
            showResult = expressionResult
        End Sub

        Sub ClearHistory
            If vbCancel = MsgBox("Do you really want to clear the history?", vbOKCancel + vbInformation + vbDefaultButton2, document.Title) Then Exit Sub
            historyStream.Close
            With CreateObject("Scripting.FileSystemObject")
                If .FileExists(historyFile) Then
                    .DeleteFile historyFile
                End If
            End With
            document.parentWindow.location.reload
        End Sub
            
        Sub ExecuteHistory
            On Error Resume Next
                Set historyStream = fso.OpenTextFile(historyFile)
                If Err Then Exit Sub
            On Error Goto 0
            While Not historyStream.AtEndOfStream
                Xecute historyStream.ReadLine
            Wend
            historyStream.Close
        End Sub
            
        Sub UpdateGuiFromConfigFile
            Dim executeHistoryOnLoad
            'set default(s)
            executeHistoryOnLoad = True
            'get values from the .config file
            On Error Resume Next
                Dim stream : Set stream = fso.OpenTextFile(configFile, ForReading)
                Execute stream.ReadAll
                stream.Close
            On Error Goto 0
            'update the gui
            autoExecHistory.checked = executeHistoryOnLoad
        End Sub

        Sub UpdateConfigFileFromGui
            Dim stream : Set stream = fso.OpenTextFile(configFile, ForWriting, CreateNew)
            stream.WriteLine "executeHistoryOnLoad = " & autoExecHistory.checked
            stream.Close
        End Sub

        Sub ToggleHelp
            If showingHelp Then
                help.innerHTML = ""
                showingHelp = False
            Else showingHelp = True
                help.innerHTML = _
                    "<ul>" & _
                        "<li> To execute a globally-scoped VBScript statement, " & _
                            "type it in the input box and press Enter. </li>" & _
                        "<li> To show the return value of a function or expression, " & _
                            "enter <pre>show &lt;expression&gt;</pre> </li>" & _
                        "<li> To cycle through the statement history, " & _
                            "press the up and down arrows. </li>" & _
                        "<li> To clear the input field, press Esc. </li>" & _
                        "<li> To toggle this message, press F1. </li>" & _
                        "<li> To open the .config file, press F9. </li>" & _
                    "</ul>"
                    window.scrollBy 0, 300
                End If
        End Sub

        Sub EditHistory
            sh.Run "notepad """ & historyFile & """"
            document.parentWindow.Close
        End Sub

        Sub ShowError(errorDescription)
            feedback.innerHTML = errorDescription
        End Sub

        Sub SetupHtmlElements
            document.Title = document.getElementsByTagName("application")(0).applicationName
            document.body.style.fontFamily = "sans-serif"
            ' table
            Set table = document.createElement("table")
            document.body.insertBefore table
            StyleTable
            AddTableRow "Statement", "Result", False 'header row
            ' statement input
            Set typedStatement = document.createElement("input")
            typedStatement.type = "text"
            typedStatement.style.width = "100%"
            document.body.insertBefore typedStatement
            ' clear history button
            Set resetButton = document.createElement("input")
            resetButton.type = "button"
            resetButton.value = "Clear history"
            Set resetButton.OnClick = GetRef("ClearHistory")
            document.body.insertBefore resetButton
            ' edit history button
            Set rewriteHistory = document.createElement("input")
            rewriteHistory.type = "button"
            rewriteHistory.value = "Edit history"
            rewriteHistory.style.marginLeft = "30px"
            Set rewriteHistory.OnClick = GetRef("EditHistory")
            document.body.insertBefore rewriteHistory
            ' check box
            Set autoExecHistDiv = document.createElement("div")
            autoExecHistDiv.style.fontSize = "smaller"
            Set autoExecHistDiv.OnMouseOver = GetRef("CheckBoxDivOnHover")
            document.body.insertBefore autoExecHistDiv
            Set autoExecHistory = document.createElement("input")
            autoExecHistory.type = "checkbox"
            Set autoExecHistory.OnClick = GetRef("UpdateConfigFileFromGui")
            autoExecHistory.style.marginRight = "10px"
            autoExecHistDiv.insertBefore autoExecHistory
            Dim ckBoxLabel : Set ckBoxLabel = document.createTextNode("Execute history on load")
            autoExecHistDiv.insertBefore ckBoxLabel
            ' error message div
            Set feedback = document.createElement("div")
            feedback.style.color = "#700"
            document.body.insertBefore feedback
            ' F1 help message div
            Set help = document.createElement("div")
            document.body.insertBefore help
            ' update html element(s) from .config file
            UpdateGuiFromConfigFile
        End Sub

        Sub StyleTable : With table.style
            .borderCollapse = "collapse"
            .fontSize = "13"
        End With : End Sub
                
        Sub StyleRow(row, erred, statementCell, resultsCell)
            With row.style
                If 0 = rowCount Then .fontWeight = "bold"
                If rowCount mod 2 Then .backgroundColor = "#eeefee"
            End With
            statementCell.style.paddingLeft = "10px"
            With resultsCell.style
                .paddingLeft = "10px"
                .paddingRight = "10px"
                If erred Then .color = "#700"
            End With
        End Sub

        Dim resetButton, rewriteHistory 'buttons
        Dim table, typedStatement, help, feedback, autoExecHistory, autoExecHistDiv 'other html elements
        Dim fso, sh, format 'other objects
        Dim historyStream 'text stream object
        Dim savedStatements, showResult 'strings
        Dim statementIndex, historyStreamStatus, rowCount 'integers
        Dim showingHelp 'boolean
        Dim historyFile
        Dim configFile
        Const EnterKey = 13
        Const UpArrow = 38
        Const DownArrow = 40
        Const Esc = 27
        Const F1 = 112
        Const F9 = 120
        Const ForReading = 1, ForWriting = 2, ForAppending = 8, CreateNew = True
        Const statusReading = 0, statusWriting = 1
        Const delimiter = "_-_"
            
        Sub Window_OnLoad
            Self.ResizeTo 660, 400
            Self.MoveTo 600, 150
            rowCount = 0
            savedStatements = ""
            statementIndex = -1
            Set fso = CreateObject("Scripting.FileSystemObject")
            Set sh = CreateObject("WScript.Shell")
            Set format = CreateObject("VBScripting.StringFormatter")
            Dim appData : appData = sh.ExpandEnvironmentStrings("%AppData%")
            Dim baseName : baseName = fso.GetBaseName(document.location.href)
            historyFile = format(Array("%s\VBScripting\%s.history", appData, baseName))
            configFile = format(Array("%s\VBScripting\%s.config", appData, baseName))
            showingHelp = False
            historyStreamStatus = statusReading
            SetupHtmlElements
            If autoExecHistory.checked Then
                ExecuteHistory
            End If
            historyStreamStatus = statusWriting
            ShowError ""
            On Error Resume Next
                Set historyStream = fso.OpenTextFile(historyFile, ForAppending, CreateNew)
                If Err Then ShowError Err.Description
                typedStatement.focus
            On Error Goto 0
        End Sub

        Sub Window_OnUnload
            On Error Resume Next
                historyStream.Close
                Set historyStream = Nothing
                Set fso = Nothing
                Set sh = Nothing
            On Error Goto 0
        End Sub
    </script>
</body>
</html>